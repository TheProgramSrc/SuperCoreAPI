plugins { 
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group 'xyz.theprogramsrc'
version '5.3.4-SNAPSHOT'
description 'The best way to create a plugin'

repositories{
    mavenCentral()
    mavenLocal()

    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.theprogramsrc.xyz/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.org/repository/maven-public' }
    maven { url 'https://repo.codemc.org/repository/nms' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'

    implementation 'com.github.cryptomorin:XSeries:8.6.0.0.1'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'me.carleslc.Simple-YAML:Simple-Yaml:1.7.2'
    implementation 'com.zaxxer:HikariCP:3.3.1'
    compileOnly 'org.xerial:sqlite-jdbc:3.36.0.2'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.17.1'
    compileOnly 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'net.lingala.zip4j:zip4j:2.9.1'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    implementation 'org.jetbrains:annotations:23.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    exclude 'META-INF/**'
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    exclude 'META-INF/**'
}

shadowJar {
    archiveBaseName.set('SuperCoreAPI')
    archiveClassifier.set('')

    relocate 'com.google.gson', 'xyz.theprogramsrc.supercoreapi.libs.google.gson'
    relocate 'org.apache.commons', 'xyz.theprogramsrc.supercoreapi.libs.apache.commons'
    relocate 'com.cryptomorin.xseries', 'xyz.theprogramsrc.supercoreapi.libs.xseries'
    relocate 'org.simpleyaml', 'xyz.theprogramsrc.supercoreapi.libs.simpleyaml'
    relocate 'org.yaml.snakeyaml', 'xyz.theprogramsrc.supercoreapi.libs.snakeyaml'
    relocate 'org.slf4j', 'xyz.theprogramsrc.supercoreapi.libs.slf4j'
    relocate 'net.lingala.zip4j', 'xyz.theprogramsrc.supercoreapi.libs.zip4j'
    relocate 'org.checkerframework', 'xyz.theprogramsrc.supercoreapi.libs.checkerframework'
    relocate 'org.intellij', 'xyz.theprogramsrc.supercoreapi.libs.intellij'
    relocate 'org.jetbrains', 'xyz.theprogramsrc.supercoreapi.libs.jetbrains'
    relocate 'javax.annotation', 'xyz.theprogramsrc.supercoreapi.libs.annotation'
    relocate 'com.zaxxer.hikari', 'xyz.theprogramsrc.supercoreapi.libs.hikari'

    minimize()
}

configurations {
    testImplementation {
        extendsFrom(compileOnly)
    }
}

sourceSets.main.java.srcDir 'src/main/java'
sourceSets.test.java.srcDir 'src/test/java'

publishing {
    repositories {
        maven {
            name = 'TheProgramSrcRepository'
            credentials.username = System.getenv('NEXUS_USERNAME')
            credentials.password = System.getenv('NEXUS_PASSWORD')
            url = uri(version.contains('-SNAPSHOT') ? 'https://repo.theprogramsrc.xyz/repository/maven-snapshots/' : 'https://repo.theprogramsrc.xyz/repository/maven-releases/')
        }
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/TheProgramSrc/SuperCoreAPI'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId 'supercoreapi'

            from components.java

            pom.withXml {
                asNode().appendNode('packaging', 'jar')
                asNode().remove(asNode().get('dependencies'))

                def dependenciesNode = asNode().appendNode('dependencies')
                def jetbrainsAnnotations = dependenciesNode.appendNode('dependency')
                jetbrainsAnnotations.appendNode('groupId', 'org.jetbrains')
                jetbrainsAnnotations.appendNode('artifactId', 'annotations')
                jetbrainsAnnotations.appendNode('version', '19.0.0')
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publish.dependsOn clean, test, jar

